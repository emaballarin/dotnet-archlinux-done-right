# Maintainer:   Emanuele Ballarin <emanuele@ballarin.cc>
# Contributor:  Maxime Gauduin <alucryd@archlinux.org>
# Contributor:  Max Liebkies <mail@maxliebkies.de>

pkgbase=dotnet-core-bin
pkgname=('dotnet-host-bin' 'dotnet-runtime-bin' 'aspnet-runtime-bin' 'dotnet-sdk-bin')
pkgver=2.2.1+102
_pkgver=2.2.1+102
pkgrel=1
arch=('x86_64')
url='https://www.microsoft.com/net/core'
license=('MIT')
makedepends=('clang' 'cmake' 'curl' 'git' 'icu' 'krb5' 'libunwind' 'lldb' 'llvm' 'lttng-ust' 'openssl' 'zlib')
options=('staticlibs')
source=(
  'https://download.visualstudio.microsoft.com/download/pr/53cace8f-2907-487e-84d7-bc7a7ba5de05/326704ffa2ef9d4dcd0db2f1da996ebb/aspnetcore-runtime-2.2.1-linux-x64.tar.gz'
  'https://download.visualstudio.microsoft.com/download/pr/6183afd2-d361-4815-9b34-0c110b93493e/0ec250c96be6a9ffdd7d55ad52c8a2d6/dotnet-sdk-2.2.102-linux-x64.tar.gz'
  'dotnet.sh'
)
sha256sums=('8a275315b36f6f66038049c34dcb4808e0fa4ece49651957f7be83d6b235f4b8'
            '70c878413c9893f8dd34160b79dba7d7153de107199febdb7b3b2ce2b7043f5a'
            'f4cb02490234b853946477f82514f0c6247b55b08b2e85fae98e99a6e6974edd')

package_dotnet-host-bin() {
  pkgdesc='A generic driver for the .NET Core Command Line Interface (2.2 version)'
  provides=('dotnet-host' 'dotnet-host-2.2' 'dotnet-host-preview' 'dotnet-host-3.0')
  conflicts=('dotnet-host' 'dotnet-host-2.2' 'dotnet-host-preview' 'dotnet-host-3.0')

  install -dm 755 "${pkgdir}"/{opt/dotnet,usr/share/licenses/dotnet-host}
  cp -dr --no-preserve='ownership' dotnet host "${pkgdir}"/opt/dotnet/
  cp -dr --no-preserve='ownership' LICENSE.txt ThirdPartyNotices.txt "${pkgdir}"/usr/share/licenses/dotnet-host
  install -Dm 755 "${srcdir}"/dotnet.sh "${pkgdir}"/usr/bin/dotnet
}

package_dotnet-runtime-bin() {
  pkgdesc='The .NET Core runtime (2.2 version)'
  depends=('dotnet-host-bin' 'icu' 'krb5' 'libunwind' 'openssl' 'zlib' 'libcurl.so')
  optdepends=('lttng-ust: CoreCLR tracing')
  provides=('dotnet-runtime' 'dotnet-runtime-2.2' 'dotnet-runtime-preview' 'dotnet-runtime-3.0')
  conflicts=('dotnet-runtime' 'dotnet-runtime-2.2' 'dotnet-runtime-preview' 'dotnet-runtime-3.0')

  install -dm 755 "${pkgdir}"/{opt/dotnet/shared,usr/share/licenses}
  cp -dr --no-preserve='ownership' shared/Microsoft.NETCore.App "${pkgdir}"/opt/dotnet/shared/
  ln -s dotnet-host "${pkgdir}"/usr/share/licenses/dotnet-runtime
}

package_aspnet-runtime-bin() {
  pkgdesc='The ASP.NET Core runtime (2.2 version)'
  depends=('dotnet-runtime-bin')
  provides=('aspnet-runtime' 'aspnet-runtime-2.2' 'aspnet-runtime-preview' 'aspnet-runtime-3.0')
  conflicts=('aspnet-runtime' 'aspnet-runtime-2.2' 'aspnet-runtime-preview' 'aspnet-runtime-3.0')

  install -dm 755 "${pkgdir}"/{opt/dotnet/shared,usr/share/licenses}
  cp -dr --no-preserve='ownership' shared/Microsoft.AspNetCore.App "${pkgdir}"/opt/dotnet/shared/
  cp -dr --no-preserve='ownership' shared/Microsoft.AspNetCore.All "${pkgdir}"/opt/dotnet/shared/
  ln -s dotnet-host "${pkgdir}"/usr/share/licenses/aspnet-runtime
}

package_dotnet-sdk-bin() {
  pkgdesc='The .NET Core SDK (2.2 version)'
  depends=('dotnet-runtime-bin')
  provides=('dotnet-sdk' 'dotnet-sdk-2.2' 'dotnet-sdk-preview' 'dotnet-sdk-3.0')
  conflicts=('dotnet-sdk' 'dotnet-sdk-2.2' 'dotnet-sdk-preview' 'dotnet-sdk-3.0')

  install -dm 755 "${pkgdir}"/{opt/dotnet,usr/share/licenses}
  cp -dr --no-preserve='ownership' sdk "${pkgdir}"/opt/dotnet/
  ln -s dotnet-host "${pkgdir}"/usr/share/licenses/dotnet-sdk
}

# vim: ts=2 sw=2 et:
